#Include "tlpp-core.th"
#Include "tlpp-rest.th"

namespace custom.portaldorepresentante.produtos

@Get("portal-do-representante/produtos/")
user function getProdutos()

    local jResponse     := JsonObject():New()       as json
    local jPath         := JsonObject():New()       as json
    local cQuery        := ""                       as character
    local cAlias        := ""                       as character
    local cFiltro       := ""                       as character
    local cPagina       := ""                       as character
    local cTamPagina    := ""                       as character
    local nContador     := 0                        as numeric
    local jItem                                     as json

    custom.portaldorepresentante.setenv.u_McSetEnv()

    jPath := oRest:getQueryRequest() // Parametros passados via query string

    cFiltro     := jPath:getJsonObject('filter')
    cPagina     := jPath:getJsonObject('page')
    cTamPagina  := jPath:getJsonObject('pageSize')
    
    cQuery := getQueryProdutos(cFiltro,cPagina,cTamPagina)

    cAlias := getNextAlias()

    MPSysOpenQuery(cQuery,cAlias)

    dbSelectArea(cAlias)
    (cAlias)->(dbGoTop())

    jResponse['items'] = {}

    nContador := 0

    while !(cAlias)->(eof()) .and. iif(!empty(cTamPagina),nContador < val(cTamPagina),.T.)

        jItem := JsonObject():New()

        jItem['codigo']             := allTrim((cAlias)->B1_COD)
        jItem['descricao']          := allTrim((cAlias)->B1_DESC)
        jItem['unidade']            := (cAlias)->B1_UM
        jItem['tipo']               := (cAlias)->B1_TIPO
        jItem['formatoEmbalagem']   := (cAlias)->B1_YFORMAT
        jItem['pesoNeto']           := (cAlias)->B1_PESO
        jItem['pesoBruto']          := (cAlias)->B1_PESBRU
        jItem['tipoSal']            := (cAlias)->B1_YTPSAL
        jItem['marca']              := allTrim((cAlias)->B1_YMARCA)

        aAdd(jResponse['items'],jItem)

        (cAlias)->(dbSkip())

        nContador++

    enddo

    jResponse['hasNext'] = !(cAlias)->(eof())

    oRest:setKeyHeaderResponse("Content-Type","application/json")
    oRest:setResponse(jResponse:toJson())

    (cAlias)->(DbCloseArea())

return


@Get("portal-do-representante/produtos/:codigo")
user function getProduto()

    local jResponse         := JsonObject():New()       as json
    local jPath             := JsonObject():New()       as json
    local aAreaSB1          := {}                       as array
    local cCodigoProduto    := ""                       as character

    custom.portaldorepresentante.setenv.u_McSetEnv()

    jPath := oRest:getPathParamsRequest() // Parametros passados via query string

    cCodigoProduto  := allTrim(jPath['codigo'])
    
    dbSelectArea("SB1")
    aAreaSB1 := SB1->(GetArea())
    SB1->(dbSetOrder(1))
    SB1->(dbGoTop())

    if SB1->(dbSeek(xFilial("SB1")+cCodigoProduto))

        jResponse['success']            := .T.
        jResponse['codigo']             := allTrim(SB1->B1_COD)
        jResponse['descricao']          := allTrim(SB1->B1_DESC)
        jResponse['unidade']            := SB1->B1_UM
        jResponse['tipo']               := SB1->B1_TIPO
        jResponse['formatoEmbalagem']   := SB1->B1_YFORMAT
        jResponse['pesoNeto']           := SB1->B1_PESO
        jResponse['pesoBruto']          := SB1->B1_PESBRU
        jResponse['tipoSal']            := SB1->B1_YTPSAL
        jResponse['marca']              := allTrim(SB1->B1_YMARCA)

    else

        jResponse["success"]    := .F.
        jResponse["message"]    := "Produto não encontrado"
        jResponse["fix"]        := "Revise o código do produto e tente novamente"
        
    endif

    oRest:setKeyHeaderResponse("Content-Type","application/json")
    oRest:setResponse(jResponse:toJson())

    restArea(aAreaSB1)

return


static function getQueryProdutos(cFiltro,cPagina,cTamPagina)

    local cQuery        := ""

    cQuery += " SELECT *
    cQuery += " FROM " + retSQLName("SB1")
    cQuery += " WHERE D_E_L_E_T_ = ' ' AND B1_MSBLQL != '1'

    if !empty(cFiltro)
        cQuery += " AND (B1_COD LIKE '%" + upper(cFiltro) + "%' OR B1_DESC LIKE '%" + upper(cFiltro) + "%' OR B1_YMARCA LIKE '%" + upper(cFiltro) + "%')
    endif

    cQuery += " ORDER BY R_E_C_N_O_

    if !empty(cPagina) .and. !empty(cTamPagina)
        cQuery += " OFFSET ("+cPagina+" - 1) * "+cTamPagina+" ROWS
        cQuery += " FETCH NEXT "+str(val(cTamPagina)+1)+" ROWS ONLY
    endif


return cQuery
